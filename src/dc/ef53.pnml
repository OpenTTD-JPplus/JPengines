spriteset(spriteset_ef53_jgr, "gfx/electric/ef53.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_ef53_imperial, "gfx/electric/ef53.png") {
	template_2cc_std(1, 32)
}

spriteset(spriteset_ef53_jnr, "gfx/electric/ef53.png") {
	template_2cc_std(1, 64)
}

spriteset(spriteset_ef59_1, "gfx/electric/ef53.png") {
	template_2cc_std(1, 96)
}

spriteset(spriteset_ef53_cc, "gfx/electric/ef53.png") {
	template_2cc_std(1, 128)
}

spriteset(spriteset_ef59_1_cc, "gfx/electric/ef53.png") {
	template_2cc_std(1, 160)
}


spriteset(spriteset_ef53_jgr_purchase, "gfx/electric/ef53.png") {
	template_purchase(1, 192)
}

spriteset(spriteset_ef53_jnr_purchase, "gfx/electric/ef53.png") {
	template_purchase(1, 224)
}


spriteset(spriteset_ef59_1_purchase, "gfx/electric/ef53.png") {
	template_purchase(89, 224)
}


switch(FEAT_TRAINS, SELF, switch_ef53_yeardep_livery, build_year) {
	0..1949: spriteset_ef53_jgr;
	spriteset_ef53_jnr;
}

switch(FEAT_TRAINS, SELF, switch_ef53_yeardep_purchase, current_year) {
	0..1949: spriteset_ef53_jgr_purchase;
	spriteset_ef53_jnr_purchase;
}

switch(FEAT_TRAINS, SELF, switch_ef53_livery, cargo_subtype) {
	1: spriteset_ef53_jgr;
	2: spriteset_ef53_imperial;
	3: spriteset_ef53_jnr;
	4: spriteset_ef53_cc;
	switch_ef53_yeardep_livery;
}

switch(FEAT_TRAINS, SELF, switch_ef53_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_YEARDEP);
	1: return string(STR_CONCAT_2, string(STR_SUFFIX_JGR), 
                                    string(STR_CONCAT_1F, 
                                     string(STR_COLOUR_BROWN)
                                    )
                                 );
	2: return string(STR_CONCAT_3, string(STR_SUFFIX_JGR), 
                                   string(STR_CONCAT_2F, 
                                    string(STR_COLOUR_BROWN), 
                                    string(STR_COLOUR_GOLD)
                                    ), string(STR_SUFFIX_IMPERIAL)
                                 );
	3: return string(STR_CONCAT_2, string(STR_SUFFIX_JNR), 
                                    string(STR_CONCAT_1F, 
                                     string(STR_COLOUR_BROWN)
                                    )
                                 );
	4: return string(STR_REFIT_CC);
	   return CB_RESULT_NO_TEXT;
	}



item (FEAT_TRAINS, ef53, 1016) {
 property {
  sprite_id: SPRITE_ID_NEW_TRAIN;
  track_type: TR_1067_DC;
  name: string(STR_NAME_EF53);
  climates_available: ALL_CLIMATES;
  introduction_date: date(1932,1,1);
  speed: 95 km/h;
  air_drag_coefficient: 0;
  tractive_effort_coefficient:    0.25;
  power: 1350 kW;
  dual_headed: 0;
  engine_class: ENGINE_CLASS_ELECTRIC;
  cargo_capacity: 1; 
  cargo_allow_refit: [PASS];
  weight: 98;
  reliability_decay: 20;
  model_life: 45;
  vehicle_life: 45;
  cost_factor: 45;
  length: 8;
  running_cost_factor: 70;
  running_cost_base: RUNNING_COST_ELECTRIC;
  }
  graphics { // graphics for engine
  additional_text:	string(STR_CONCAT_4S, 
                        string(STR_DESC_POWER_DC15),
                        string(STR_DESC_NARROWGAUGE),
                        string(STR_DESC_USAGE_EP),
                        string(STR_CONCAT_2C, 
                           string(STR_SUFFIX_JGR),
                           string(STR_SUFFIX_JNR)
                    )
                  );
  cargo_capacity: return 0;
  cargo_subtype_text: switch_ef53_subtype_text;
  default: switch_ef53_livery;
  purchase: switch_ef53_yeardep_purchase;
  }
}


switch(FEAT_TRAINS, SELF, switch_ef59_1_livery, cargo_subtype) {
	1: spriteset_ef59_1_cc;
	spriteset_ef59_1;
}

switch(FEAT_TRAINS, SELF, switch_ef59_1_subtype_text, cargo_subtype) {
	0: return string(STR_CONCAT_3, string(STR_SUFFIX_JNR), 
                                   string(STR_CONCAT_2F, 
                                    string(STR_COLOUR_BROWN), 
                                    string(STR_COLOUR_YELLOW)
                                    ), string(STR_SUFFIX_BANKER)
                                 );
	1: return string(STR_REFIT_CC);
	   return CB_RESULT_NO_TEXT;
	}

switch (FEAT_TRAINS, SELF, switch_ef59_1_name_purchase, getbits(extra_callback_info1, 8, 8)) { 
  1: return CB_RESULT_NO_TEXT; 
  return string(STR_NAME_EF59);
  } 
switch (FEAT_TRAINS, SELF, switch_ef59_1_name, getbits(extra_callback_info1, 0, 8)) { 
  0x20: switch_ef59_1_name_purchase; 
  return CB_RESULT_NO_TEXT;
  }


item (FEAT_TRAINS, ef59_1, 1916) {
 property {
  sprite_id: SPRITE_ID_NEW_TRAIN;
  track_type: TR_1067_DC;
  name: string(STR_NAME_EF59_1);
  climates_available: ALL_CLIMATES;
  introduction_date: date(1963,1,1);
  speed: 95 km/h;
  air_drag_coefficient: 0;
  tractive_effort_coefficient:    0.25;
  power: 1350 kW;
  dual_headed: 0;
  engine_class: ENGINE_CLASS_ELECTRIC;
  cargo_capacity: 1; 
  cargo_allow_refit: [PASS];
  weight: 110;
  reliability_decay: 20;
  model_life: 45;
  vehicle_life: 45;
  cost_factor: 45;
  length: 8;
  running_cost_factor: 70;
  running_cost_base: RUNNING_COST_ELECTRIC;
  variant_group: 1016;
  }
  graphics { // graphics for engine
  additional_text:	string(STR_CONCAT_4S, 
                        string(STR_DESC_POWER_DC15),
                        string(STR_DESC_NARROWGAUGE),
                        string(STR_DESC_USAGE_BANK),
                        string(STR_CONCAT_1C, 
                           string(STR_SUFFIX_JNR)
                    )
                  );
  cargo_capacity: return 0;
  cargo_subtype_text: switch_ef59_1_subtype_text;
  default: switch_ef59_1_livery;
  purchase: spriteset_ef59_1_purchase;
  name: switch_ef59_1_name;
  }
}