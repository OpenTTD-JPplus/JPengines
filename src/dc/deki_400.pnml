spriteset(spriteset_deki_400_black, "gfx/private/deki401.png") {
	template_2cc_std(1, 1)
}
spriteset(spriteset_deki_400_yellow, "gfx/private/deki401.png") {
	template_2cc_std(1, 32)
}

spriteset(spriteset_deki_400_blue, "gfx/private/deki401.png") {
	template_2cc_std(1, 64)
}

spriteset(spriteset_deki_400_cc, "gfx/private/deki401.png") {
	template_2cc_std(1, 96)
}

spriteset(spriteset_deki_400_black_purchase, "gfx/private/deki401.png") {
	template_purchase(1, 128)
}

spriteset(spriteset_deki_400_yellow_purchase, "gfx/private/deki401.png") {
	template_purchase(89, 128)
}

spriteset(spriteset_deki_400_blue_purchase, "gfx/private/deki401.png") {
	template_purchase(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_deki_400_purchase, current_year) {
	0..1960: spriteset_deki_400_black_purchase;
	1960..1993: spriteset_deki_400_yellow_purchase;
	spriteset_deki_400_blue_purchase;
}

switch(FEAT_TRAINS, SELF, switch_deki_400_yeardep_livery, build_year) {
	0..1960: spriteset_deki_400_black;
	1960..1993: spriteset_deki_400_yellow;
	spriteset_deki_400_blue;
}

switch(FEAT_TRAINS, SELF, switch_deki_400_livery, cargo_subtype) {
	1: spriteset_deki_400_black;
	2: spriteset_deki_400_yellow;
	3: spriteset_deki_400_blue;
	4: spriteset_deki_400_cc;
	switch_deki_400_yeardep_livery;
}



switch(FEAT_TRAINS, SELF, switch_deki_400_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_YEARDEP);
	1: return string(STR_CONCAT_2, string(STR_SUFFIX_AICHI), 
                                  string(STR_CONCAT_1F, 
                                    string(STR_COLOUR_BLACK)
                                    )
                                 );
	2: return string(STR_CONCAT_2, string(STR_SUFFIX_MEITETSU), 
                                  string(STR_CONCAT_2F, 
                                    string(STR_COLOUR_BLACK), 
                                    string(STR_COLOUR_YELLOW)
                                    )
                                 );
	3: return string(STR_CONCAT_2, string(STR_SUFFIX_MEITETSU), 
                                  string(STR_CONCAT_2F, 
                                    string(STR_COLOUR_BLUE), 
                                    string(STR_COLOUR_YELLOW)
                                    )
                                 );
	4: return string(STR_REFIT_CC);
	   return CB_RESULT_NO_TEXT;
	}




//deki
item (FEAT_TRAINS, deki_400, 1005) {
 property {
  sprite_id: SPRITE_ID_NEW_TRAIN;
  track_type: TR_1067_DC;
  name: string(STR_NAME_DEKI_400);
  climates_available: ALL_CLIMATES;
  introduction_date: date(1930,1,1);
  speed: 65 km/h;
  air_drag_coefficient: 0;
  tractive_effort_coefficient:    0.25;
  power: 400 kW;
  dual_headed: 0;
  engine_class: ENGINE_CLASS_ELECTRIC;
  cargo_capacity: 1;
  cargo_allow_refit: [PASS];
  weight: 40;
  reliability_decay: 20;
  model_life: 70;
  vehicle_life: 60;
  cost_factor: 22;
  length: 5;
  running_cost_factor: 32;
  running_cost_base: RUNNING_COST_ELECTRIC;
  misc_flags: bitmask(TRAIN_FLAG_2CC);
  }
  graphics { // graphics for engine
  additional_text:	string(STR_CONCAT_4S, 
                        string(STR_DESC_POWER_DC15),
                        string(STR_DESC_NARROWGAUGE),
                        string(STR_DESC_USAGE_LF),
                        string(STR_CONCAT_2C, 
                           string(STR_SUFFIX_AICHI),
                           string(STR_SUFFIX_MEITETSU)
                    )
                  );

  cargo_capacity: return 0;
  cargo_subtype_text: switch_deki_400_subtype_text;
  default: switch_deki_400_livery;
  purchase: switch_deki_400_purchase;
  }
}