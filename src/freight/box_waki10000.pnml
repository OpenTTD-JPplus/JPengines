

spriteset(spriteset_waki_10000_blue, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_waki_10000_green, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 32)
}

spriteset(spriteset_waki_10000_brown, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 64)
}

//purchase
spriteset(spriteset_waki_10000_jnr_purchase, "gfx/freight/waki_5000.png") {
	template_purchase(1, 352)
}

spriteset(spriteset_waki_10000_jrf_purchase, "gfx/freight/waki_5000.png") {
	template_purchase(89, 352)
}

switch(FEAT_TRAINS, SELF, switch_waki_10000_yeardep_purchase, current_year) {
	0..1987: spriteset_waki_10000_jnr_purchase;
	spriteset_waki_10000_jrf_purchase;
}

random_switch (FEAT_TRAINS, SELF, random_waki_10000) {
	1: spriteset_waki_10000_brown;
	1: spriteset_waki_10000_blue;
	2: spriteset_waki_10000_green;
}

switch(FEAT_TRAINS, SELF, switch_waki_10000_livery, cargo_subtype) {
	1: spriteset_waki_10000_blue;
	2: spriteset_waki_10000_green;
	3: spriteset_waki_10000_brown;
	random_waki_10000;
}

switch(FEAT_TRAINS, SELF, switch_waki_10000_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_RANDOM);
	1: return string(STR_CONCAT_1F, string(STR_COLOUR_BLUE));
	2: return string(STR_CONCAT_1F, string(STR_COLOUR_GREEN));
	3: return string(STR_CONCAT_1F, string(STR_COLOUR_BROWN));
	   return CB_RESULT_NO_TEXT;
	}

switch(FEAT_TRAINS, SELF, switch_waki_10000_loadstack, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0:  switch_waki_10000_livery;
    1:  spritegroup_boxvan8_doors_load;
}

item (FEAT_TRAINS, waki_10000, 1288) {

property {
	name: string(STR_NAME_WAKI_10000);
	introduction_date: date(1967,1,1);
	default_cargo_type: GOOD;
	cargo_capacity: 60; 
	refittable_cargo_classes: bitmask(CC_MAIL, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
	cargo_allow_refit: [BEER];
	cargo_disallow_refit: [PASS, COAL, LVST, WOOD, IORE, CORE, AORE, BRCK, CLAY, GRVL, LIME, OLSD, POTA, SAND, SCMT, SGBT, SGCN, TOUR, RFPR];
	cost_factor: 50;
	running_cost_factor: 8;
	running_cost_base: RUNNING_COST_STEAM;
	misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
	loading_speed: 20;
	}
graphics {
	additional_text:	string(STR_CONCAT_3S, 
							string(STR_DESC_NARROWGAUGE),
							string(STR_DESC_USAGE_FRT_EXP),
							string(STR_CONCAT_2C, 									
								string(STR_SUFFIX_JNR),
								string(STR_SUFFIX_JRF)
							)
						);
	speed: switch_speedboost_1;
	purchase_speed: return 100;
	cargo_capacity: return 60;
	default: switch_waki_10000_loadstack;
	purchase: switch_waki_10000_yeardep_purchase;
	cargo_subtype_text: switch_waki_10000_subtype_text;
	}
}
