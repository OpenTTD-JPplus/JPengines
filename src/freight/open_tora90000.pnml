spriteset(spriteset_tora_90000_empty, "gfx/freight/tora_90000.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_tora_90000_half, "gfx/freight/tora_90000.png") {
	template_2cc_anim(1, 32)
}

spriteset(spriteset_tora_90000_full, "gfx/freight/tora_90000.png") {
	template_2cc_anim(1, 64)
}

spriteset(spriteset_tora_90000_tarp, "gfx/freight/tora_90000.png") {
	template_2cc_std(1, 96)
}

spriteset(spriteset_tora_90000_purchase, "gfx/freight/tora_90000.png") {
	template_purchase(1, 128)
}



spritegroup spritegroup_tora_90000_bulk {
    loading: [spriteset_tora_90000_empty, spriteset_tora_90000_half, spriteset_tora_90000_full];
    loaded:  [spriteset_tora_90000_empty, spriteset_tora_90000_half, spriteset_tora_90000_full];
}

spritegroup spritegroup_tora_90000_covered {
    loading: [spriteset_tora_90000_empty, spriteset_tora_90000_half, spriteset_tora_90000_full];
    loaded:  [spriteset_tora_90000_empty, spriteset_tora_90000_tarp];
}



switch(FEAT_TRAINS, SELF, switch_tora_90000_cargo_class, cargo_classes) {
	bitmask(CC_BULK): spritegroup_tora_90000_bulk;
	bitmask(CC_COVERED): spritegroup_tora_90000_covered;
	spritegroup_tora_90000_covered;
}


item (FEAT_TRAINS, tora_90000, 1280) {

property {
	sprite_id: SPRITE_ID_NEW_TRAIN;
	track_type: NAAN;
	introduction_date: date(1950,1,1);
	climates_available: ALL_CLIMATES;
	name: string(STR_NAME_TORA_90000);
	speed: 75 km/h;
	air_drag_coefficient: 0;
	power: 0;
	dual_headed: 0;
	cargo_capacity: 35; 
	refit_cost: 0;
//	default_cargo_type: COAL;
	loading_speed: 10;
//	refittable_cargo_classes: bitmask(CC_BULK, CC_COVERED, CC_PIECE_GOODS); 
	non_refittable_cargo_classes: 0;
	cargo_allow_refit: [GRAI, FICR, WOOD, WOOL, BEAN, SGCN, SGBT, OLSD, MAIZ, PEAT, SEED, FRUT, FRVG, TATO, WDCH, TBCO];
	cargo_disallow_refit: [COAL, IORE, CORE, PORE, CMNT];
	weight: 11;
	cost_factor: 22;
	running_cost_factor: 4;
	running_cost_base: RUNNING_COST_STEAM;
	length: 5;
	misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT);
	variant_group: 1929;
	}
graphics {
	additional_text:	string(STR_CONCAT_3S, 
							string(STR_DESC_NARROWGAUGE),
							string(STR_DESC_USAGE_FRT_CHIP),
							string(STR_CONCAT_2C, 									
								string(STR_SUFFIX_JNR),
								string(STR_SUFFIX_JRF)
							)
						);

	speed: switch_speedboost_1;
	purchase_speed: return 75;
	cargo_capacity: return 35;
	default: switch_tora_90000_cargo_class;
	purchase: spriteset_tora_90000_purchase;
	colour_mapping: switch_generic_recolour;
	}
}