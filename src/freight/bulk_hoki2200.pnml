spriteset(spriteset_hoki_2200_cream, "gfx/freight/hoki_covered.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_hoki_2200_white, "gfx/freight/hoki_covered.png") {
	template_2cc_std(1, 32)
}


spriteset(spriteset_hoki_2200_purchase, "gfx/freight/hoki_covered.png") {
	template_purchase(1, 96)
}


random_switch(FEAT_TRAINS, SELF, random_hoki_2200_cream) {
	1: spriteset_hoki_2200_cream;
	1: spriteset_hoki_2200_white;
	}


switch(FEAT_TRAINS, SELF, switch_hoki_2200_livery, cargo_subtype) {
	1: spriteset_hoki_2200_cream;
	2: spriteset_hoki_2200_white;
	random_hoki_2200_cream;
}

switch(FEAT_TRAINS, SELF, switch_hoki_2200_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_RANDOM);
	1: return string(STR_CONCAT_1F, string(STR_COLOUR_CREAM));
	2: return string(STR_CONCAT_1F, string(STR_COLOUR_WHITE));
	   return CB_RESULT_NO_TEXT;
	}



item (FEAT_TRAINS, hoki_2200, 1291) {

property {
	introduction_date: date(1966,1,1);
	climates_available: ALL_CLIMATES;
	name: string(STR_NAME_HOKI_2200);
	speed: 85 km/h;
	loading_speed: 40;
//	refittable_cargo_classes: bitmask(CC_COVERED); 
	default_cargo_type: FOOD;
	non_refittable_cargo_classes: 0;
	cargo_allow_refit: [FOOD, GRAI, WHEA, BEAN, SGCN, SGBT, OLSD, MAIZ, PEAT, SEED, BAKE, SUGR, SALT, SASH, POTA];
	cargo_disallow_refit: [WOOL, WOOD, SGCN, TBCO];
	misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT);
	cost_factor: 40;
	running_cost_factor: 6;
	running_cost_base: RUNNING_COST_STEAM;
	}
graphics {
	additional_text:	string(STR_CONCAT_3S, 
							string(STR_DESC_NARROWGAUGE),
							string(STR_DESC_USAGE_FRT_GRAI),
							string(STR_CONCAT_1C, 									
								string(STR_SUFFIX_JNR)
							)
						);

	speed: switch_speedboost_1;
	purchase_speed: return 85;
	cargo_capacity: return 58;
	default: switch_hoki_2200_livery;
	cargo_subtype_text: switch_hoki_2200_subtype_text;
	purchase: spriteset_hoki_2200_purchase;
	}
}