spriteset(spriteset_wa_1_stripe_closed, "gfx/freight/wa_1.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_wa_1_stripe_empty, "gfx/freight/wa_1.png") {
	template_2cc_anim(1, 32)
}

spriteset(spriteset_wa_1_stripe_full, "gfx/freight/wa_1.png") {
	template_2cc_anim(1, 64)
}

spriteset(spriteset_wa_1_black_closed, "gfx/freight/wa_1.png") {
	template_2cc_std(1, 96)
}

spriteset(spriteset_wa_1_black_empty, "gfx/freight/wa_1.png") {
	template_2cc_anim(1, 128)
}

spriteset(spriteset_wa_1_black_full, "gfx/freight/wa_1.png") {
	template_2cc_anim(1, 160)
}

spriteset(spriteset_wa_1_brown_closed, "gfx/freight/wa_1.png") {
	template_2cc_std(1, 192)
}

spriteset(spriteset_wa_1_brown_empty, "gfx/freight/wa_1.png") {
	template_2cc_anim(1, 224)
}

spriteset(spriteset_wa_1_brown_full, "gfx/freight/wa_1.png") {
	template_2cc_anim(1, 256)
}


spriteset(spriteset_wa_1_jgr_purchase, "gfx/freight/wa_1.png") {
	template_purchase(1, 288)
}

spriteset(spriteset_wa_1_jnr_purchase, "gfx/freight/wa_1.png") {
	template_purchase(89, 288)
}



spritegroup spritegroup_wa_1_stripe_load {
    loading: [spriteset_wa_1_stripe_empty, spriteset_wa_1_stripe_full, spriteset_wa_1_stripe_closed];
    loaded:  [spriteset_wa_1_stripe_closed];
}


spritegroup spritegroup_wa_1_black_load {
    loading: [spriteset_wa_1_black_empty, spriteset_wa_1_black_full, spriteset_wa_1_black_closed];
    loaded:  [spriteset_wa_1_black_closed];
}

spritegroup spritegroup_wa_1_brown_load {
    loading: [spriteset_wa_1_brown_empty, spriteset_wa_1_brown_full, spriteset_wa_1_brown_closed];
    loaded:  [spriteset_wa_1_brown_closed];
}



random_switch(FEAT_TRAINS, SELF, random_wa_1_black) {
	1: spritegroup_wa_1_black_load;
	1: spritegroup_wa_1_stripe_load;
	}
	
random_switch(FEAT_TRAINS, SELF, random_wa_1_brown) {
	1: spritegroup_wa_1_black_load;
	1: spritegroup_wa_1_brown_load;
	}

switch(FEAT_TRAINS, SELF, switch_wa_1_yeardep, build_year) {
	0..1928: random_wa_1_brown;
	random_wa_1_black;
	}



switch(FEAT_TRAINS, SELF, switch_wa_1_livery, cargo_subtype) {
	1: spritegroup_wa_1_black_load;
	2: spritegroup_wa_1_stripe_load;
	3: spritegroup_wa_1_brown_load;
	switch_wa_1_yeardep;
}

switch(FEAT_TRAINS, SELF, switch_wa_1_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_RANDOM);
	1: return string(STR_CONCAT_1F, string(STR_COLOUR_BLACK));
	2: return string(STR_CONCAT_2F, string(STR_COLOUR_BLACK), string(STR_COLOUR_YELLOW));
	3: return string(STR_CONCAT_1F, string(STR_COLOUR_BROWN));
	   return CB_RESULT_NO_TEXT;
	}



item (FEAT_TRAINS, wa_1, 32) {

property {
	name: string(STR_NAME_WA_1);
	introduction_date: date(1870,1,1);
	default_cargo_type: GOOD;
	cargo_capacity: 20;
	speed: 65 km/h;
	refittable_cargo_classes: bitmask(CC_COVERED, CC_MAIL, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED, CC_ARMOURED); 
	non_refittable_cargo_classes: 0;
	cargo_disallow_refit: [PASS, COAL, WOOD, IORE, CORE, AORE, CLAY, GRVL, LIME, OLSD, POTA, SAND, SGBT, SGCN, TOUR, VEHI, KAOL];
	cost_factor: 14;
	running_cost_factor: 2;
	running_cost_base: RUNNING_COST_STEAM;
	misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT);
	loading_speed: 20;
	}
graphics {
	additional_text:	string(STR_CONCAT_3S, 
							string(STR_DESC_NARROWGAUGE),
							string(STR_DESC_USAGE_FRT_BOX),
							string(STR_CONCAT_3C, 									
								string(STR_SUFFIX_JGR),
								string(STR_SUFFIX_JNR),
								string(STR_SUFFIX_PRIVATE)
							)
						);

	speed: switch_speedboost_1;
	purchase_speed: return 65;
	cargo_capacity: return 20;
	default: switch_wa_1_livery;
	cargo_subtype_text: switch_wa_1_subtype_text;
	purchase: spriteset_wa_1_jgr_purchase;
//	colour_mapping: switch_generic_recolour;
	}
}