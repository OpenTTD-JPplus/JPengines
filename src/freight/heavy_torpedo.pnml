spriteset(spriteset_torpedo_old_front, "gfx/freight/steel_wagons.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_torpedo_old_rear, "gfx/freight/steel_wagons.png") {
	template_2cc_std(1, 32)
}

spriteset(spriteset_torpedo_new_front, "gfx/freight/steel_wagons.png") {
	template_2cc_std(180, 1)
}

spriteset(spriteset_torpedo_new_rear, "gfx/freight/steel_wagons.png") {
	template_2cc_std(180, 32)
}


spriteset(spriteset_torpedo_empty, "gfx/freight/steel_wagons.png") {
	template_2cc_anim(1, 64)
}

spriteset(spriteset_torpedo_half, "gfx/freight/steel_wagons.png") {
	template_2cc_anim(1, 96)
}

spriteset(spriteset_torpedo_full, "gfx/freight/steel_wagons.png") {
	template_2cc_anim(1, 128)
}



spriteset(spriteset_torpedo_yawata_old_purchase, "gfx/freight/steel_wagons.png") {
	template_purchase(1, 256)
}

spriteset(spriteset_torpedo_yawata_new_purchase, "gfx/freight/steel_wagons.png") {
	template_purchase(180, 256)
}

spriteset(spriteset_torpedo_nippon_purchase, "gfx/freight/steel_wagons.png") {
	template_purchase(89, 256)
}

switch(FEAT_TRAINS, SELF, switch_torpedo_purchase, current_year) {
	0..1950: spriteset_torpedo_yawata_old_purchase;
	1950..1990: spriteset_torpedo_yawata_new_purchase;
	spriteset_torpedo_nippon_purchase;
	}

/* --- Articulated part callback  --- */
switch(FEAT_TRAINS, SELF, switch_torpedo_articulated_part, extra_callback_info1) {
	1: return torpedo_car_middle;
    2: return torpedo_car_rear;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch(FEAT_TRAINS, SELF, switch_torpedo_front_yeardep, build_year) {
	0..1950: spriteset_torpedo_old_front;
	spriteset_torpedo_new_front;
}

switch(FEAT_TRAINS, SELF, switch_torpedo_rear_yeardep, build_year) {
	0..1950: spriteset_torpedo_old_rear;
	spriteset_torpedo_new_rear;
}



switch(FEAT_TRAINS, SELF, switch_torpedo_loading, cargo_count) {
	0..19: spriteset_torpedo_empty;
	20..39: spriteset_torpedo_half;
	40..60: spriteset_torpedo_full;
	CB_FAILED;
}

item (FEAT_TRAINS, torpedo_car_middle, 1422) {
	property {
		sprite_id: SPRITE_ID_NEW_TRAIN;
		track_type: NAAN;
		name: string(STR_NAME_TORPEDO);
		climates_available: NO_CLIMATE;
		cargo_capacity: 59; 
		refit_cost: 0;
		default_cargo_type: IRON;
		loading_speed: 40;
		cargo_age_period: 320;
//		refittable_cargo_classes: bitmask(CC_COVERED, CC_BULK); 
//		non_refittable_cargo_classes: 0; 
		cargo_allow_refit: [IRON, METL]; 
		introduction_date: date(1920,1,1);
		length: 6;
		visual_effect_and_powered: (VISUAL_EFFECT_DISABLE);
	}
	graphics {
		cargo_capacity: return 59;
		default: switch_torpedo_loading;
	}
}

item (FEAT_TRAINS, torpedo_car_rear, 1423) {
	property {
		sprite_id: SPRITE_ID_NEW_TRAIN;
		track_type: NAAN;
		name: string(STR_NAME_TORPEDO);
		climates_available: NO_CLIMATE;
		introduction_date: date(1920,1,1);
		length: 3;
		visual_effect_and_powered: (VISUAL_EFFECT_DISABLE);
	}
	graphics {
		default: switch_torpedo_rear_yeardep;
	}
}



item (FEAT_TRAINS, torpedo_car, 1421) {
	property {
		sprite_id: SPRITE_ID_NEW_TRAIN;
		track_type: NAAN;
		name: string(STR_NAME_TORPEDO);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1920,1,1);
		speed: 65 km/h;
		air_drag_coefficient: 0;
		power: 0;
		dual_headed: 0;
		cargo_capacity: 1; 
		refit_cost: 0;
		default_cargo_type: IRON;
		loading_speed: 20;
//		refittable_cargo_classes: bitmask(CC_COVERED, CC_BULK); 
//		non_refittable_cargo_classes: 0; 
		cargo_allow_refit: [IRON, METL]; 
//		cargo_disallow_refit: [WDPR, WOOL];
		weight: 120;
		cost_factor: 100;
	running_cost_factor: 10;
	running_cost_base: RUNNING_COST_STEAM;
		length: 3;
//		misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_2CC);
	}
	graphics {
		additional_text:	string(STR_CONCAT_4S, 
								string(STR_DESC_NARROWGAUGE),
								string(STR_DESC_USAGE_FRT_STEL),
								string(STR_CONCAT_2C, 
									string(STR_SUFFIX_YAWATA),
									string(STR_SUFFIX_PRIVATE)
								),
								string(STR_DESC_BRAKE_NA)
							);

		cargo_capacity: return 1;
		default: switch_torpedo_front_yeardep;
		purchase: switch_torpedo_purchase;
		articulated_part: switch_torpedo_articulated_part;
	}
}