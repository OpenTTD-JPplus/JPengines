spriteset(spriteset_koki_5500_purchase, "gfx/freight/koki_test.png") {
	template_purchase(1, 352)
}

//base
spriteset(spriteset_koki_5500_empty_a, "gfx/freight/koki_test.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_koki_5500_empty_b, "gfx/freight/koki_test.png") {
	template_2cc_std(1, 32)
}

//handrail

spriteset(spriteset_koki_5500_handrail, "gfx/freight/koki_test.png") {
	template_2cc_std(1, 320)
}



random_switch (FEAT_TRAINS, SELF, random_koki_5500_brown) {
	2: spriteset_koki_5500_empty_a;
	1: spriteset_koki_5500_empty_b;
}




switch(FEAT_TRAINS, SELF, switch_koki_5500_base_loadstack, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 7 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0:  random_koki_5500_brown;
    1:  switch_conts_cargo_load_end;
	2:	switch_conts_cargo_load_middle;
	3:	switch_conts_cargo_load_middle;
	4:	switch_conts_cargo_load_end;
	5:  spriteset_koki_5500_handrail;
	6:  switch_taillights_8;
}


item (FEAT_TRAINS, koki_5500, 1412) {
	property {
		sprite_id: SPRITE_ID_NEW_TRAIN;
		track_type: NAAN;
		name: string(STR_NAME_KOKI_5500);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1959,1,1);
		speed: 85 km/h;
		air_drag_coefficient: 0;
		power: 0;
		dual_headed: 0;
		cargo_capacity: 60; 
		refit_cost: 0;
		default_cargo_type: GOOD;
		loading_speed: 35;
		refittable_cargo_classes: bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_LIQUID); 
		non_refittable_cargo_classes: 0; 
		cargo_allow_refit: [MAIL, GOOD, OIL_, PAPR, FOOD, RUBR, FRUT, WATR, SUGR, TOYS, BATT, STEL, METL, SWET, TOFF, COLA, CTCD, BUBL, PLST, FZDR, BEER, COPR, FICR, BDMT, CERA, CMNT, DYES, ENSP, FERT, FISH, FMSP, FRVG, GLAS, MILK, MNSP, PETR, PLAS, RCYC, RFPR, SULP, WDPR, WOOL, VEHI, EOIL, ACID, CHLO, SLAG]; 
		cargo_disallow_refit: [PASS, COAL, LVST, GRAI, WOOD, IORE, VALU, WHEA, GOLD, MAIZ, CORE, DIAM, AORE, CERE, CLAY, GRVL, LIME, OLSD, POTA, SAND, SCMT, SGBT, SGCN, TOUR, JAVA];
		weight: 20;
		cost_factor: 30;
	running_cost_factor: 5;
	running_cost_base: RUNNING_COST_STEAM;
		length: 8;
		misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
	}
	graphics {
		additional_text:	string(STR_CONCAT_3S, 
							string(STR_DESC_NARROWGAUGE),
							string(STR_DESC_USAGE_FRT_CONT),
							string(STR_CONCAT_2C, 									
								string(STR_SUFFIX_JNR),
								string(STR_SUFFIX_JRF)
							)
						);
		speed: switch_speedboost_1;
		purchase_speed: return 85;
		cargo_capacity: return 60;
		default: switch_koki_5500_base_loadstack;
		purchase: spriteset_koki_5500_purchase;
		refit_cost: CB_RESULT_AUTOREFIT;
	}
}