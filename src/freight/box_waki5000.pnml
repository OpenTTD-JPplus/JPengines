
spriteset(spriteset_waki_5000_greenstripe, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 96)
}

spriteset(spriteset_waki_5000_brown1, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 128)
}

spriteset(spriteset_waki_5000_bluestripe, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 160)
}

spriteset(spriteset_waki_5000_brown2, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 192)
}

spriteset(spriteset_waki_5000_black, "gfx/freight/waki_5000.png") {
	template_2cc_std(1, 224)
}


//purchase
spriteset(spriteset_waki_5000_jnr_purchase, "gfx/freight/waki_5000.png") {
	template_purchase(1, 384)
}

spriteset(spriteset_waki_5000_jrf_purchase, "gfx/freight/waki_5000.png") {
	template_purchase(89, 384)
}

switch(FEAT_TRAINS, SELF, switch_waki_5000_yeardep_purchase, current_year) {
	0..1987: spriteset_waki_5000_jnr_purchase;
	spriteset_waki_5000_jrf_purchase;
}


random_switch (FEAT_TRAINS, SELF, random_waki_5000) {
	1: spriteset_waki_10000_brown;
	1: spriteset_waki_5000_greenstripe;
	4: spriteset_waki_5000_brown1;
	5: spriteset_waki_5000_brown2;
	2: spriteset_waki_5000_black;
}

random_switch (FEAT_TRAINS, SELF, random_waki_5000_brown) {
	1: spriteset_waki_5000_brown1;
	2: spriteset_waki_5000_brown2;
}



switch(FEAT_TRAINS, SELF, switch_waki_5000_livery, cargo_subtype) {
	1: random_waki_5000_brown;
	2: spriteset_waki_5000_greenstripe;
	3: spriteset_waki_5000_black;
	4: spriteset_waki_5000_bluestripe;
	random_waki_5000;
}

switch(FEAT_TRAINS, SELF, switch_waki_5000_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_RANDOM);
	1: return string(STR_CONCAT_1F, string(STR_COLOUR_BROWN));
	2: return string(STR_CONCAT_2F, string(STR_COLOUR_BROWN), string(STR_COLOUR_GREEN));
	3: return string(STR_CONCAT_1F, string(STR_COLOUR_BLACK));
	4: return string(STR_CONCAT_2F, string(STR_COLOUR_LTBLUE), string(STR_COLOUR_BLUE));
	   return CB_RESULT_NO_TEXT;
	}

switch(FEAT_TRAINS, SELF, switch_waki_5000_loadstack, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0:  switch_waki_5000_livery;
    1:  spritegroup_boxvan8_doors_load;
}

item (FEAT_TRAINS, waki_5000, 1953) {
property {
	sprite_id: SPRITE_ID_NEW_TRAIN;
	track_type: TR_1067_NE;
	introduction_date: date(1964,1,1);
	climates_available: ALL_CLIMATES;
	name: string(STR_NAME_WAKI_5000);
	speed: 85 km/h;
	air_drag_coefficient: 0;
	power: 0;
	dual_headed: 0;
	cargo_capacity: 50; 
	refit_cost: 0;
	default_cargo_type: GOOD;
	refittable_cargo_classes: bitmask(CC_COVERED, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED); 
	loading_speed: 20;
	cargo_allow_refit: [GRAI, FICR, WOOD, WOOL, BEAN, SGCN, SGBT, OLSD, MAIZ, PEAT, SEED, FRUT, FRVG, TATO, WDCH, TBCO];
	cargo_disallow_refit: [PASS, COAL, WOOD, IORE, CORE, AORE, CLAY, GRVL, LIME, OLSD, POTA, SAND, SGBT, SGCN, TOUR, KAOL];
	non_refittable_cargo_classes: 0;
	weight: 15;
	cost_factor: 40;
	running_cost_factor: 6;
	running_cost_base: RUNNING_COST_STEAM;
	length: 8;
	misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
	}
graphics {
	additional_text:	string(STR_CONCAT_3S, 
							string(STR_DESC_NARROWGAUGE),
							string(STR_DESC_USAGE_FRT_BOX),
							string(STR_CONCAT_3C, 									
								string(STR_SUFFIX_JNR),
								string(STR_SUFFIX_JRF),
								string(STR_SUFFIX_PRIVATE)
							)
						);

	speed: switch_speedboost_1;
	purchase_speed: return 85;
	cargo_capacity: return 80;
	default: switch_waki_5000_loadstack;
	purchase: switch_waki_5000_yeardep_purchase;
	cargo_subtype_text: switch_waki_5000_subtype_text;
	}
}