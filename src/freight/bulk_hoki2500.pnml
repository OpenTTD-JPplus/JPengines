spriteset(spriteset_hoki_2500_red_empty, "gfx/freight/hoki_2500.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_hoki_2500_red_quarter, "gfx/freight/hoki_2500.png") {
	template_2cc_anim(1, 32)
}

spriteset(spriteset_hoki_2500_red_half, "gfx/freight/hoki_2500.png") {
	template_2cc_anim(1, 64)
}

spriteset(spriteset_hoki_2500_red_full, "gfx/freight/hoki_2500.png") {
	template_2cc_anim(1, 96)
}

spriteset(spriteset_hoki_2500_red_cover, "gfx/freight/hoki_2500.png") {
	template_2cc_std(1, 128)
}


spriteset(spriteset_hoki_2500_jnr_purchase, "gfx/freight/hoki_2500.png") {
	template_purchase(1, 256)
}

spriteset(spriteset_hoki_2500_jrf_purchase, "gfx/freight/hoki_2500.png") {
	template_purchase(89, 256)
}




spritegroup spritegroup_hoki_2500_red_bulk {
    loading: [spriteset_hoki_2500_red_empty, spriteset_hoki_2500_red_quarter, spriteset_hoki_2500_red_half, spriteset_hoki_2500_red_full];
    loaded:  [spriteset_hoki_2500_red_empty, spriteset_hoki_2500_red_quarter, spriteset_hoki_2500_red_half, spriteset_hoki_2500_red_full];
}

spritegroup spritegroup_hoki_2500_red_cover {
    loading: [spriteset_hoki_2500_red_empty, spriteset_hoki_2500_red_quarter, spriteset_hoki_2500_red_half, spriteset_hoki_2500_red_full];
    loaded:  [spriteset_hoki_2500_red_empty, spriteset_hoki_2500_red_cover];
}

random_switch(FEAT_TRAINS, SELF, random_hoki_2500_cover) {
	2: spritegroup_hoki_2500_red_cover;
	1: spritegroup_hoki_2500_red_bulk;
	}


switch(FEAT_TRAINS, SELF, switch_hoki_2500_lid_cargoclass, cargo_classes) {
	bitmask(CC_BULK): spritegroup_hoki_2500_red_bulk;
	spritegroup_hoki_2500_red_cover;
}

switch(FEAT_TRAINS, SELF, switch_hoki_2500_lid_cargotype, cargo_type_in_veh) {
	AORE: return random_hoki_2500_cover;
	CORE: return random_hoki_2500_cover;
	PORE: return random_hoki_2500_cover;
	SAND: return random_hoki_2500_cover;
	GRVL: return random_hoki_2500_cover;
	LIME: return random_hoki_2500_cover;
	CLAY: return random_hoki_2500_cover;
	SLAG: return random_hoki_2500_cover;
	switch_hoki_2500_lid_cargoclass;
}




switch(FEAT_TRAINS, SELF, switch_hoki_2500_livery, cargo_subtype) {
	1: spritegroup_hoki_2500_red_bulk;
	2: spritegroup_hoki_2500_red_cover;
	switch_hoki_2500_lid_cargoclass;
}

switch(FEAT_TRAINS, SELF, switch_hoki_2500_subtype_text, cargo_subtype) {
	0: return string(STR_REFIT_RANDOM);
	1: return string(STR_CONCAT_1F, string(STR_SUFFIX_FOPEN));
	2: return string(STR_CONCAT_1F, string(STR_SUFFIX_FCOVER));
	   return CB_RESULT_NO_TEXT;
	}


switch(FEAT_TRAINS, SELF, switch_hoki_2500_purchase, current_year) {
	0..1987: spriteset_hoki_2500_jnr_purchase;
	spriteset_hoki_2500_jrf_purchase;
	}


item (FEAT_TRAINS, hoki_2500, 42) {

property {
	introduction_date: date(1970,1,1);
	name: string(STR_NAME_HOKI_2500);
	speed: 85 km/h;
	refittable_cargo_classes: bitmask(CC_BULK, CC_COVERED); 
	non_refittable_cargo_classes: 0;
	cargo_disallow_refit: [GRAI, WHEA, BEAN, SGBT, WDPR, FICR, WOOL, POWR, BAKE, FURN, GLAS, MPTS, PAPR, TEXT, SGCN, TBCO, SUGR];
	misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT);
	cost_factor: 40;
	running_cost_factor: 6;
	running_cost_base: RUNNING_COST_STEAM;
	loading_speed: 40;
	}
graphics {
	additional_text:	string(STR_CONCAT_3S, 
							string(STR_DESC_NARROWGAUGE),
							string(STR_DESC_USAGE_FRT_LMST),
							string(STR_CONCAT_2C, 									
								string(STR_SUFFIX_JNR),
								string(STR_SUFFIX_JRF)
							)
						);

	speed: switch_speedboost_1;
	purchase_speed: return 85;
	cargo_capacity: return 55;
	default: switch_hoki_2500_livery;
	purchase: switch_hoki_2500_purchase;
	cargo_subtype_text: switch_hoki_2500_subtype_text;
	colour_mapping: switch_generic_recolour;
	}
}