//Universal door sprites for all 8/8 box vans

spriteset(spriteset_boxvan8_doors_blank, "gfx/paxmail/mani44.png") {
	template_2cc_std(1, 64)
}

spriteset(spriteset_boxvan8_doors_empty, "gfx/paxmail/mani44.png") {
	template_2cc_std(1, 96)
}

spriteset(spriteset_boxvan8_doors_half, "gfx/paxmail/mani44.png") {
	template_2cc_std(1, 128)
}

spritegroup spritegroup_boxvan8_doors_load {
    loading: [spriteset_boxvan8_doors_empty, spriteset_boxvan8_doors_half, spriteset_boxvan8_doors_blank];
    loaded:  [spriteset_boxvan8_doors_blank];
}

//MANI 44

spriteset(spriteset_mani_44, "gfx/paxmail/mani44.png") {
	template_2cc_std(1, 1)
}

spriteset(spriteset_mani_44_euroliner, "gfx/paxmail/mani44.png") {
	template_2cc_std(1, 32)
}



spriteset(spriteset_mani_44_jnr_purchase, "gfx/paxmail/mani44.png") {
	template_purchase(1, 160)
}

spriteset(spriteset_mani_44_jr_purchase, "gfx/paxmail/mani44.png") {
	template_purchase(89, 160)
}

switch(FEAT_TRAINS, SELF, switch_mani_44_purchase_yeardep, current_year) {
    0..1987: spriteset_mani_44_jnr_purchase;
    spriteset_mani_44_jr_purchase;
    }

switch(FEAT_TRAINS, SELF, switch_mani_44_livery, cargo_subtype) {
	1: spriteset_mani_44_euroliner;
	spriteset_mani_44;
}


switch(FEAT_TRAINS, SELF, switch_mani_44_subtype_text, cargo_subtype) {
	0: return string(STR_CONCAT_2, string(STR_SUFFIX_JNR), 
                                    string(STR_CONCAT_1F, 
                                     string(STR_COLOUR_BLUE)
                                    )
                                 );
	1: return string(STR_CONCAT_3, string(STR_SUFFIX_JRC),
                                   string(STR_CONCAT_2F, 
                                    string(STR_COLOUR_WHITE), 
                                    string(STR_COLOUR_BLUE)
                                    ), string(STR_SUFFIX_EUROLINER)
                                 );
	   return CB_RESULT_NO_TEXT;
	}


//stacked sprites

switch(FEAT_TRAINS, SELF, switch_mani_44_loadstack, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0:  switch_mani_44_livery;
    1:  spritegroup_boxvan8_doors_load;
}


item (FEAT_TRAINS, mail_mani_44, 1390) {
	property {
		sprite_id: SPRITE_ID_NEW_TRAIN;
		track_type: TR_1067_NE;
		name: string(STR_NAME_MAIL_44);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1970,1,1);
		speed: 109.5 km/h;
		air_drag_coefficient: 0;
		power: 0;
		dual_headed: 0;
		cargo_capacity: 100; 
		refit_cost: 0;
		default_cargo_type: MAIL;
		loading_speed: 50;
		refittable_cargo_classes: bitmask(CC_MAIL, CC_EXPRESS); 
		non_refittable_cargo_classes: 0; 
		weight: 24;
		cost_factor: 52;
		running_cost_factor: 8;
		running_cost_base: RUNNING_COST_STEAM;
		length: 8;
		misc_flags: bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP, TRAIN_FLAG_SPRITE_STACK);
	}
	graphics {
		additional_text:	string(STR_CONCAT_3S, 
								string(STR_DESC_NARROWGAUGE),
								string(STR_DESC_USAGE_MAIL_EXF),
								string(STR_CONCAT_2C, 
									string(STR_SUFFIX_JNR),
									string(STR_SUFFIX_JRC)
								)
							);

		default: switch_mani_44_loadstack;
		purchase: switch_mani_44_purchase_yeardep;
		cargo_subtype_text: switch_mani_44_subtype_text;
		
	}
}